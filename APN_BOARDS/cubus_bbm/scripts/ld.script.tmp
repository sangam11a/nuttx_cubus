MEMORY
{
    flash (rx) : ORIGIN = 0x08000000, LENGTH = 2032K
    sram (rwx) : ORIGIN = 0x20000000, LENGTH = 192K
    ccsram (rwx) : ORIGIN = 0x10000000, LENGTH = 64K
}
OUTPUT_ARCH(arm)
ENTRY(__start)
EXTERN(_vectors)
EXTERN(abort)
EXTERN(_bootdelay_signature)
SECTIONS
{
 .text : {
  _stext = ABSOLUTE(.);
  *(.vectors)
  . = ALIGN(32);
  _bootdelay_signature = ABSOLUTE(.);
  FILL(0xffecc2925d7d05c5)
  . += 8;
  KEEP(*(.app_descriptor))
  *(.text .text.*)
  *(.fixup)
  *(.gnu.warning)
  *(.rodata .rodata.*)
  *(.gnu.linkonce.t.*)
  *(.got)
  *(.gcc_except_table)
  *(.gnu.linkonce.r.*)
  _etext = ABSOLUTE(.);
 } > flash
        .init_section : {
                _sinit = ABSOLUTE(.);
                KEEP(*(.init_array .init_array.*))
                _einit = ABSOLUTE(.);
        } > flash
 .ARM.extab : {
  *(.ARM.extab*)
 } > flash
 __exidx_start = ABSOLUTE(.);
 .ARM.exidx : {
  *(.ARM.exidx*)
 } > flash
 __exidx_end = ABSOLUTE(.);
 _eronly = ABSOLUTE(.);
 .data : {
  _sdata = ABSOLUTE(.);
  *(.data .data.*)
  *(.gnu.linkonce.d.*)
  CONSTRUCTORS
  _edata = ABSOLUTE(.);
 } > sram AT > flash
 .bss : {
  _sbss = ABSOLUTE(.);
  *(.bss .bss.*)
  *(.gnu.linkonce.b.*)
  *(COMMON)
  . = ALIGN(4);
  _ebss = ABSOLUTE(.);
 } > sram
 .stab 0 : { *(.stab) }
 .stabstr 0 : { *(.stabstr) }
 .stab.excl 0 : { *(.stab.excl) }
 .stab.exclstr 0 : { *(.stab.exclstr) }
 .stab.index 0 : { *(.stab.index) }
 .stab.indexstr 0 : { *(.stab.indexstr) }
 .comment 0 : { *(.comment) }
 .debug_abbrev 0 : { *(.debug_abbrev) }
 .debug_info 0 : { *(.debug_info) }
 .debug_line 0 : { *(.debug_line) }
 .debug_pubnames 0 : { *(.debug_pubnames) }
 .debug_aranges 0 : { *(.debug_aranges) }
}
